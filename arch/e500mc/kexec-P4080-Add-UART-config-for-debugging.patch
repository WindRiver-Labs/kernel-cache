From 384915251967ad3be2f85a32efd7ecbb10cb0177 Mon Sep 17 00:00:00 2001
From: Benjamin Walsh <benjamin.walsh@windriver.com>
Date: Wed, 29 Sep 2010 11:15:37 -0400
Subject: [PATCH 170/233] kexec/P4080: Add UART config for debugging

Configure the UART so that it is accessible for debugging after
tearing down the TLBs and leaving only an identity map.

Signed-off-by: Benjamin Walsh <benjamin.walsh@windriver.com>
---
 arch/powerpc/kernel/misc_32.S |   71 +++++++++++++++++++++++++++++++++++++++++
 1 files changed, 71 insertions(+), 0 deletions(-)

diff --git a/arch/powerpc/kernel/misc_32.S b/arch/powerpc/kernel/misc_32.S
index 1e24d1e..b757f8d 100644
--- a/arch/powerpc/kernel/misc_32.S
+++ b/arch/powerpc/kernel/misc_32.S
@@ -875,6 +875,77 @@ all_cpus:
 	/* from this point address translation is turned off */
 	/* and interrupts are disabled */
 
+#ifdef CONFIG_P4080_DS
+	/* uart setup for all CPUs:
+	 *   map 64K@0xffe110000(RPN) in ESEL[6] to 0xfe110000(EPN) */
+
+	#define UART_EPN	0xfe11
+	#define UART_RPN	0xfe11
+	#define UART_ESEL	0x1006
+	#define UART_WIMGE	0x000a	/* xIxGx */
+	#define UART_PERMS	0x003f	/* S+rwx, U+rwx */
+	#define UART_SIZE_64K	0x0300
+	#define UART_LSR	0xc505
+	#define UART_IRQ	0xc501
+	#define UART_THR	0xc500
+
+	/* fetch MAS1 from ESEL0 */
+	lis	r6, 0x1000
+	mtspr	SPRN_MAS0, r6
+	isync
+	tlbre
+	sync
+	isync
+
+	mfspr	r6, SPRN_MAS1
+	isync
+
+	lis	r7, 0x00ff
+	ori	r7, r7, 0x1000
+	and	r8, r7, r6		/* r8 contains MAS1 TID+TS */
+
+	/* setup MAS0, ESEL6 */
+	lis	r6, UART_ESEL
+	mtspr	SPRN_MAS0, r6
+	isync
+
+	/* setup MAS1, ESEL6 */
+	lis	r6, 0xc000		/* valid and IPROT */
+	ori	r6, r6, UART_SIZE_64K	/* 64K page */
+	or	r6, r6, r8		/* use same TS, TID as ESEL0 */
+	mtspr	SPRN_MAS1, r6
+	isync
+
+	/* map 0xffe110000 @ 0xfe110000, cache inhibited and guarded */
+	lis	r6, UART_EPN		/* EPN@h */
+	ori	r6, r6, UART_WIMGE	/* EPN@l + WIMGE: xIxGx */
+	mtspr	SPRN_MAS2, r6
+	isync
+
+	lis	r6, UART_RPN		/* RPN@h */
+	ori	r6, r6, UART_PERMS	/* RPN@l + all permissions on */
+	mtspr	SPRN_MAS3, r6
+	isync
+
+	li	r6, 0xf			/* high 4-bits of RPN in MAS7 */
+	mtspr	SPRN_MAS7, r6
+	isync
+
+	tlbwe
+	isync
+	msync
+	sync
+
+	/* stop interrupts */
+	lis	r6, UART_EPN
+	ori	r6, r6, UART_IRQ
+	li	r0, 0
+	stb	r0, 0(r6)
+	sync
+
+	/* end - uart setup */
+
+#endif	/* CONFIG_P4080_DS */
 
 #ifdef CONFIG_SMP
 	/* if not CPU0, jump to spin */
-- 
1.7.0.4

