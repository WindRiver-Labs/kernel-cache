From a7c6ce1ed738f174f89398a4b3c6261a3ee7bf28 Mon Sep 17 00:00:00 2001
From: Benjamin Walsh <benjamin.walsh@windriver.com>
Date: Tue, 30 Mar 2010 18:29:15 -0400
Subject: [PATCH] fsl_8572ds/kexec: UART support for debugging/purgatory

Set an identity mapping for the UART so that purgatory output
can be seen. Can be used for debugging in the future as well.

Signed-off-by: Benjamin Walsh <benjamin.walsh@windriver.com>
---
 arch/powerpc/kernel/misc_32.S |   68 +++++++++++++++++++++++++++++++++++++++++
 1 files changed, 68 insertions(+), 0 deletions(-)

diff --git a/arch/powerpc/kernel/misc_32.S b/arch/powerpc/kernel/misc_32.S
index 4f6a66d..8f5d678 100644
--- a/arch/powerpc/kernel/misc_32.S
+++ b/arch/powerpc/kernel/misc_32.S
@@ -1069,6 +1069,74 @@ all_cpus:
 1:
 	/* from this point address translation is turned off */
 	/* and interrupts are disabled */
+	
+/* uart0 setup: identity map in ESEL[6] for CCSRBAR 1M@0xffe00000 */
+
+#define UART_EPN	0xffe0
+#define UART_RPN	0xffe0
+#define UART_ESEL	0x1006
+#define UART_WIMGE	0x000a	/* xIxGx */
+#define UART_PERMS	0x003f	/* S+rwx, U+rwx */
+#define UART_SIZE_1M	0x0500
+#define UART_LSR	0x4505
+#define UART_IRQ	0x4501
+#define UART_THR	0x4500
+
+	/* fetch MAS1 from ESEL0 */
+	lis	r6, 0x1000
+	mtspr	SPRN_MAS0, r6
+	isync
+	tlbre
+	sync
+	isync
+
+	mfspr	r6, SPRN_MAS1
+	isync
+
+	lis	r7, 0x00ff
+	ori	r7, r7, 0x1000
+	and	r8, r7, r6	/* r8 contains MAS1 TID+TS */
+
+	/* setup MAS0, ESEL6 */
+	lis	r6, UART_ESEL
+	mtspr	SPRN_MAS0, r6
+	isync
+
+	/* setup MAS1, ESEL6 */
+	lis	r6, 0xc000		/* valid and IPROT */
+	ori	r6, r6, UART_SIZE_1M	/* 1M page */
+	or	r6, r6, r8		/* use same TS, TID as ESEL0 */
+	mtspr	SPRN_MAS1, r6
+	isync
+
+	/* identity map @ 0xffe00000, cache inhibited and guarded */
+	lis	r6, UART_EPN		/* EPN@h */
+	ori	r6, r6, UART_WIMGE	/* EPN@l + WIMGE: xIxGx */
+	mtspr	SPRN_MAS2, r6
+	isync
+
+	lis	r6, UART_RPN		/* RPN@h */
+	ori	r6, r6, UART_PERMS	/* RPN@l + all permissions on */
+	mtspr	SPRN_MAS3, r6
+	isync
+
+	lis	r6, 0		/* RPN in MAS7 */
+	mtspr	SPRN_MAS7, r6
+	isync
+
+	tlbwe
+	isync
+	msync
+	sync
+
+	/* stop interrupts */
+	lis	r6, UART_EPN
+	ori	r6, r6, UART_IRQ
+	li	r0, 0
+	stb	r0, 0(r6)
+	sync
+
+/* end - uart setup */
 
 #ifdef CONFIG_SMP
 	/* if not CPU0, jump to spin */
-- 
1.6.0.4

